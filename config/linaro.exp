# The master configuration file should be here

set sysroot /opt/linaro/sysroot-linaro_eglibc-2_18-aarch64-linux-gnu
set besysroot /opt/linaro/sysroot-linaro_eglibc-2_18-aarch64_be-linux-gnu

set newlib sysroot-newlib.git~linaro_newlib-branch-aarch64-none-elf/
set benewlib sysroot-newlib.git~linaro_newlib-branch-aarch64_be-none-elf/

global env
if {[info exists env(PREFIX_UNDER_TEST)]} {
    set prefix "$env(PREFIX_UNDER_TEST)"
    set flags ""
    if {[info exists env(FLAGS_UNDER_TEST)]} {
	set flags "$env(FLAGS_UNDER_TEST)"
    }
    if {[info exists env(SYSROOT_UNDER_TEST)]} {
	set sysroot "$env(SYSROOT_UNDER_TEST)"
	set flags "${flags} --sysroot=$env(SYSROOT_UNDER_TEST)"
    }

    set GCC_UNDER_TEST "[set prefix]gcc $flags"
    set GXX_UNDER_TEST "[set prefix]g++ $flags"
    set GFORTRAN_UNDER_TEST "[set prefix]gfortran $flags"
    set OBJC_UNDER_TEST "[set prefix]gcc $flags"
    set GOC_UNDER_TEST "[set prefix]goc $flags"
    set GNAT_UNDER_TEST "[set prefix]gnat $flags"
}

if {[info exists env(SYSROOT_UNDER_TEST)]} {
    set native ""
} else {
    set native "native"
}

# Configure "unix" board, which is what DJ uses for native testing.
# This is a similar board to tcwg-remote.exp, but we can't name board
# tcwg-local.exp, because DejaGNU hard-wired to use "unix" for local testing.
proc tcwg_local { } {
    global board_info
    # Compilation timeout 10min
    set board_info(unix,gcc,timeout) 600
    # Execution timeout 3min.
    set board_info(unix,timeout) 180
    # Unfortunatelly, current DejaGNU can't reliably kill timed-out
    # processes, so wrap them in timeout.
    set board_info(unix,exec_shell) "timeout --preserve-status -k 30s 210s"

    return "unix"
}

# Configure "qemu" board to run locally and use user-mode QEMU.
proc qemu { } {
    global board_info target_triplet sysroot
    set qemu_arch "[lindex [split $target_triplet -] 0]"
    case "$qemu_arch" in {
	{ armv[78]l } {
	    set qemu_arch "arm"
	}
    }
    set target [tcwg_local]
    set board_info($target,exec_shell) "$board_info($target,exec_shell) setarch x86_64 -R qemu-$qemu_arch -cpu any -R 0 -L $sysroot"

    return $target
}

set myname [get_local_hostname]
set hostname [info hostname]

proc linaro_lab { kind } {
    global tcl_platform myname

    case "$tcl_platform(user)" in {
	{ *buildslave } {
	    set user "infra"
	}
	default {
	    set user "dev"
	}
    }

    case "$myname" in {
	{ ex40-* } {
	    set lab "hetzner"
	}
	default {
	    set lab "tcwglab"
	}
    }

    return "$user-$lab-$kind"
}

case "$target_triplet" in { 
    { "arm-*linux-gnueabi*" "armv[78]l-*linux-gnueabi*" } {
	case "$native$myname" in {
	    { *tcwgrob* ripple* darkstar* } {
		set target_list { "tk1" }
	    }
	    { native* } {
		set target_list [tcwg_local]
	    }
	    default {
		if { [info exists env(SCHROOT_TEST)]
		     && $env(SCHROOT_TEST) == "yes" } {
		    set target_list [linaro_lab armv8]
		} else {
		    set target_list [qemu]
		}
            }
	}
    }
    { "arm-*-eabi*" } {
	global SIM
	# CPU and sysroot are set through the environment
	set SIM "qemu-arm"
	set target_list { "arm-qemu" } 
    }
    { "armeb-*-eabi*" } {
	global SIM
	# CPU and sysroot are set through the environment
	set SIM "qemu-armeb"
	set target_list { "arm-qemu" } 
    }
    { "aarch64*-*elf*" } {
	set target_list { "v8model" } 
	set env(FOUNDATION_MODEL) "/linaro/Foundation_Platformpkg/models/Linux64_GCC-4.7/Foundation_Platform"
	case "$hostname" in {
	    { *.moongulch.net *.senecass.com } {
		set env(FOUNDATION_MODEL) "/work/linaro/foundation_v8pkg/Foundation_v8"
		set target_list { "aarch64-qemu" }
	    }
	}
    }
    { "aarch64-*linux*" } {
	case "$native$myname" in {
	    { *tcwgrob* ripple* darkstar* } {
		set target_list { "tx1" }
	    }
	    { native* } {
		set target_list [tcwg_local]
	    }
	    default {
		if { [info exists env(SCHROOT_TEST)]
		     && $env(SCHROOT_TEST) == "yes" } {
		    set target_list [linaro_lab armv8]
		} else {
		    set target_list [qemu]
		}
            }
	}
    }
    { "x86_64-*linux-gnu" "i686-*linux-gnu" } {
	case "$native$myname" in {
	    { native* } {
		set target_list [tcwg_local]
	    }
	    default {
		if { [info exists env(SCHROOT_TEST)]
		     && $env(SCHROOT_TEST) == "yes" } {
		    set target_list [linaro_lab x86_64]
		} else {
		    set target_list [tcwg_local]
		}
	    }
	}
    }
    { "*linux-gnu*" } {
	set target_list [qemu]
    }
    default {
	puts "No target hardware for $target_triplet"
    }
}
